plugins {
	java
	id("org.springframework.boot") version "3.1.4"
	id("io.spring.dependency-management") version "1.1.3"
	id ("org.liquibase.gradle") version "2.2.0"
	application
	jacoco
}

group = "hexlet.code"
version = "0.0.1-SNAPSHOT"

java {
	sourceCompatibility = JavaVersion.VERSION_17
}

configurations {
	compileOnly {
		extendsFrom(configurations.annotationProcessor.get())
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")
//	implementation("org.springframework.boot:spring-boot-starter-security")
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("com.h2database:h2:2.1.214")
	implementation("org.postgresql:postgresql:42.6.0")
	implementation("org.liquibase:liquibase-core:4.23.2")

	liquibaseRuntime("org.liquibase:liquibase-core:4.23.2")
	liquibaseRuntime("org.liquibase:liquibase-groovy-dsl:3.0.3")
	liquibaseRuntime ("info.picocli:picocli:4.6.1")
	liquibaseRuntime ("mysql:mysql-connector-java:5.1.34")
	liquibaseRuntime (group="javax.xml.bind", name="jaxb-api", version="2.3.1")
	liquibaseRuntime("ch.qos.logback:logback-core:1.2.3")
	liquibaseRuntime("ch.qos.logback:logback-classic:1.2.3")
	liquibaseRuntime ("org.liquibase.ext:liquibase-hibernate5:3.6")
	liquibaseRuntime (sourceSets.main.get().output)
	liquibaseRuntime ("org.springframework.boot:spring-boot-starter-data-jpa")
//	liquibaseRuntime ("com.h2database:h2:2.1.214")

	compileOnly("org.projectlombok:lombok")
	annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
	annotationProcessor("org.projectlombok:lombok")
	testImplementation("org.springframework.boot:spring-boot-starter-test")
	testImplementation("org.springframework.security:spring-security-test")
}

tasks.withType<Test> {
	useJUnitPlatform()
}

application {
	mainClass.set("hexlet.code.AppApplication")
}

tasks.register("stage") {
	dependsOn("installDist")
	doLast {
	}
}

tasks.jacocoTestReport {
	dependsOn(tasks.test) // tests are required to run before generating the report
	reports {
		xml.required.set(true)
		csv.required.set(false)
		html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
	}
}

tasks.diffChangelog {
	dependsOn(tasks.compileJava)
}

liquibase {
	activities.register("main") {
		this.arguments = mapOf(
				"changelogFile" to "src/main/resources/db/changelog/db.changelog-master.yaml",
				"url" to "jdbc:h2:./devDB",
				"referenceDriver" to "liquibase.ext.hibernate.database.connection.HibernateDriver",
				"referenceUrl" to "hibernate:spring:hexlet.code.model?dialect=org.hibernate.dialect.H2Dialect&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy",
				"username" to "sa",
				"password" to "sa"
		)
	}
}


